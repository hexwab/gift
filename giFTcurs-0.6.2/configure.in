dnl Process this file with autoconf to produce a configure script.
AC_PREREQ(2.52g)
AC_INIT(giFTcurs, 0.6.2, giftcurs-devel@nongnu.org, giFTcurs)
AC_REVISION($Revision: 1.75 $)
AC_CONFIG_SRCDIR(src/giftcurs.h)
AC_CONFIG_HEADERS(config.h)
AM_INIT_AUTOMAKE([1.7])

dnl Checks for programs.
AC_PROG_CC
AC_PROG_INSTALL

dnl Checks for libraries.
ALL_LINGUAS="ca da de en es fr it nl pl pt_BR sv"
AM_GNU_GETTEXT([], [need-ngettext], [])

AC_CHECK_CURSES
if test "$has_curses" = false ; then
	AC_ERROR(You don't seem to have the curses headers installed)
fi
AM_CONDITIONAL(HAS_WIDE_CURSES, test "$has_wide_curses" = yes)

AM_PATH_GLIB_2_0(2.0.0, , AC_ERROR(You need glib-2.0 to compile giFTcurs))

AC_CHECK_LIB(socket, socket)

AC_ARG_ENABLE(libgpm,
	AC_HELP_STRING([--disable-libgpm],[disable usage of libgpm]),, enable_libgpm=yes)
if test x$enable_libgpm = xyes; then
	AC_CHECK_LIB(gpm, Gpm_Open, enable_libgpm=yes, enable_libgpm=no)
fi

dnl Checks for header files, types, and structures.
AC_CHECK_HEADERS(getopt.h sys/vfs.h sys/mount.h sys/statvfs.h regex.h termios.h sys/ioctl.h sys/un.h)

dnl Checks for compiler characteristics.
AC_NO_CPP_PRECOMP
AX_TRY_GCC_FLAGS([-Wstrict-prototypes -Wwrite-strings -Wpointer-arith -Winline -Wall])

dnl Checks for library functions.
AC_CHECK_FUNCS(socket,,
	AC_ERROR(socket must be available in order to build.))
AC_CHECK_FUNCS(strnlen statfs getopt_long hstrerror gai_strerror)
AC_CHECK_FUNCS(getaddrinfo,,[AC_CHECK_LIB(bind, getaddrinfo)] [AC_CHECK_LIB(nsl, gethostbyname)])

dnl Parse --enable-feature options
AC_ARG_ENABLE(mouse,
	AC_HELP_STRING([--disable-mouse],[disable mouse support]),, enable_mouse=yes)
if test x$enable_mouse != xyes; then
	AC_DEFINE(DISABLE_MOUSE, 1, [Disable mouse support])
fi

AC_ARG_ENABLE(internal-mouse,
	AC_HELP_STRING([--disable-internal-mouse],[disable internal mouse parsing]),, enable_internal_mouse=yes)
if test x$enable_internal_mouse != xyes; then
	AC_DEFINE(DISABLE_INTERNAL_MOUSE, 1, [Disable internal mouse parsing])
fi

AC_CONFIG_FILES([
	Makefile
	src/Makefile
	intl/Makefile
	po/Makefile.in
])
AC_OUTPUT

echo "
 Version:         ${VERSION}
 Prefix:          ${prefix}
 CFLAGS:          ${CFLAGS}

 i18n support:    ${USE_NLS}
 Mouse support:   ${enable_mouse}
 gpm support:     ${enable_libgpm}
 UTF-8 support:   ${has_wide_curses}
"
